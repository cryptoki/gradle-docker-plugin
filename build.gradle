import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.8'
}

subprojects {
    ext {
        artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
        bintrayDryRun = false
    }

    apply plugin: 'groovy'
    apply plugin: 'com.github.ben-manes.versions'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    def dependencyVersions = [
            'org.apache.commons:commons-compress:1.15',
            'org.codehaus.groovy:groovy:2.4.12',
            'org.codehaus.groovy:groovy-json:2.4.12',
            'org.codehaus.groovy:groovy-all:2.4.12',
            'org.jetbrains.kotlin:kotlin-reflect:1.1.51',
            'org.jetbrains.kotlin:kotlin-stdlib:1.1.51',
            'org.slf4j:slf4j-api:1.7.25',
    ]

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            force dependencyVersions
        }
    }
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
    distributionType = Wrapper.DistributionType.ALL
}
